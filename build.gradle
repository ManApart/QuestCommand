buildscript {
    ext.kotlin_version = '1.3.72'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'kotlin'

group = 'org.rak.manapart'
version = 'dev'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    main {
        java {
            srcDirs 'src/main/kotlin'
            srcDirs 'src/tools/kotlin'
        }
        resources {
            srcDir 'src/main/resource'
        }
    }

    integrationTest {
        java {
            srcDirs 'src/test-integration/kotlin'
        }
        resources {
            srcDir 'src/test-integration/resource'
            srcDir 'src/main/resource'
        }

        compileClasspath += sourceSets.main.output + configurations.testCompile
        runtimeClasspath += output + compileClasspath + configurations.testRuntime
    }

    tools {
        java {
            srcDirs 'src/main/kotlin'
            srcDirs 'src/tools/kotlin'
        }

        compileClasspath += sourceSets.main.output + configurations.testCompile
        runtimeClasspath += output + compileClasspath + configurations.testRuntime
    }
}

repositories {
    maven { url "https://repo.maven.apache.org/maven2" }
    mavenCentral()
}

dependencies {
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: '1.3.61'
    compile group: 'org.reflections', name: 'reflections', version: '0.9.10'
    compile group: 'org.jline', name: 'jline', version: '3.8.0'
    compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: '2.9.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit', version: '1.2.51'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    mustRunAfter test
}

task buildData(dependsOn: 'classes', type: JavaExec) {
    main = 'building.AppBuilder'
    classpath = sourceSets.tools.runtimeClasspath
}

jar {
    manifest {
        attributes 'Main-Class': 'MainKt'
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}